# Custom .cshrc
# Written by Karim Hamidou - on 10/27/07
#

# Set an emacs-like editing mode :
bindkey -e

# Exchange the two characters before the cursor -*- UNDOCUMENTED -*- :)
# This emulate  from gosling emacs (i.e : swap the two previous letters and
# not the current letter and the previous letter (this is emacs standard
# behaviour).
bindkey ^T gosmacs-transpose-chars

bindkey "^R" i-search-back
bindkey "^S" i-search-fwd

set autolist
set fignore = ( .o .~ .pyc)

set history = 2000
# Save and merge with existing saved
set savehist = (2000 merge)
set histfile = ~/.tcsh_history

setenv VISUALWORKS /home/karim/vw7.7.1nc
setenv NPM_CONFIG_PREFIX "$HOME/.npm_bins"

# latex package directory
setenv TEXMFHOME ~/.texmf

# specify custom ruby gems dir
setenv GEM_HOME ~/.gems/

# Set an appropriate path
set path = (\
    $path\
    $HOME/.gems/bin\
    $HOME/.bin\
    $NPM_CONFIG_PREFIX/bin\
    /usr/local/go/bin\
    $HOME/softs/sbt/bin\
    $HOME/softs/scala-2.10.2/bin\
    $HOME/softs/arcanist/arcanist/bin\
    $HOME/travail/password-vault/bin\
)

# Make less a better place to live.
# -N : displays line numbers
# -Q : disable ringing
setenv LESS "-Q"
setenv EDITOR "emacs -nw"

if($TERM == "xterm") then
    setenv TERM "xterm-256color"
endif

# Make "pushd " equivalent to "pushd ~"
set pushdtohome=1

# Toogle on pushd's silent mode
set pushdsilent=1

# Prompt if rm * is executed.
set rmstar=1

# Stop those _highly_ annoying beeps
set nobeep

# Set autolist
# For instance, ch<TAB> will give a list of the commands beginning with ch.
set autolist

# List all the jobs after a C-z
set listjobs

# Define the helpcommand alias - the command to run when the user types
# a command, and then M-h, to get its explanation.
alias helpcommand man

# Define the dirs alias - to get a more precise directory stack listing.
alias dirs dirs -v

# Save the directory stack at each logout (keeps only 15 entries)
#set savedirs=15

# Some utility aliases 
alias m make
alias emacs emacs23 -nw
alias l less
alias n mc
alias sl ls
alias telnel telnet  # This one is particularly annoying.
alias cls clear
alias mdir mkdir -p  # This alias supersedes mtool's mdir but who uses msdos ?
alias ls ls-F --color=auto # This undocumented feature provide color for ls-F,
                           # a built-in implementation of ls.
alias s /bin/ls --hide "*.pyc" --hide "*~" --color

# git aliases
alias co git checkout
alias br git branch
alias ch git cherry-pick
alias cm git commit
alias st git status
alias pu git push
alias gd "git diff --color | less -r"

alias pk pkill
alias hgrep "history | grep"

alias pig ping google.com

# Commodity aliases for apt-get :
alias apts apt-cache search
alias apti sudo apt-get install
alias aptr sudo apt-get remove

alias fif find . -exec grep \!:1 '{}' \; -print

alias nautilus nautilus --no-desktop

alias w-c wicd-curses
alias sflew ssh neyret.fr

alias vg vagrant
alias vu vagrant up
alias vs vagrant ssh
alias gentags ctags -R .
alias ack ack-grep
